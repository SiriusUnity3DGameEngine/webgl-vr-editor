// Do not use mediump, causes vertices to wiggle on iOS
precision highp float;

uniform sampler2D texture;

uniform mat4 matrixModelview;
uniform mat4 matrixProjection;
uniform vec4 eye;

uniform vec4 lensFrustum;
uniform vec4 noLensFrustum;
uniform vec4 viewport;
uniform vec4 inverseDistortion;
uniform float flip;

attribute vec3 center3;
attribute vec3 position3;
attribute vec4 color4;

varying float _mask;
varying vec2 _coord2;
varying vec4 _color4;

// Frustum coordinates
const int LEFT = 0;
const int TOP = 1;
const int RIGHT = 2;
const int BOTTOM = 3;

// Viewport coordinates
const int X = 0;
const int Y = 1;
const int W = 2;
const int H = 3;

// Cell values
const float DIRT = 1.0;
const float JUMP = 2.0;
const float ROCK = 3.0;
const float WATER = 4.0;

////////////////////////////////////////////////////////////////////////////////

export void maskVertex() {
  _mask = position3.z;
  gl_Position = vec4(position3.x * flip, position3.y, 0, 1);
}

export void maskFragment() {
  gl_FragColor = vec4(_mask);
}

////////////////////////////////////////////////////////////////////////////////

vec4 worldPosition() {
  return matrixProjection * matrixModelview * vec4(mix(position3, center3, clamp(dot(eye.xyz, center3) - eye.w, 0.0, 1.0)), 1);
}

vec4 decodeColor() {
  if (color4.z < (DIRT + 0.5) / 255.0) return vec4(0.75, 0.5, 0.25, 1);
  if (color4.z < (JUMP + 0.5) / 255.0) return vec4(1, 1, 0, 1);
  if (color4.z < (ROCK + 0.5) / 255.0) return vec4(0.75, 0.75, 0.75, 1);
  if (color4.z < (WATER + 0.5) / 255.0) return vec4(0, 0.25, 0.75, 1);
  return vec4(1);
}

export void worldVertex() {
  _color4 = decodeColor();
  _coord2 = color4.xy;
  gl_Position = worldPosition();
}

export void worldVertexDistorted() {
  vec4 position = worldPosition();
  float w = abs(position.w);

  // Apply the distortion correction directly in the vertex shader since that's
  // much faster than rendering to a texture and distorting the texture. Make
  // sure to temporarily undo the perspective divide by the "w" coordinate.
  // Then multiply "w" back so the fragment shader is free of dependent reads.
  float p = mix(lensFrustum[LEFT], lensFrustum[RIGHT], (0.5 + position.x / w * 0.5 * flip - viewport[X]) / viewport[W]);
  float q = mix(lensFrustum[BOTTOM], lensFrustum[TOP], (0.5 + position.y / w * 0.5 - viewport[Y]) / viewport[H]);
  float r2 = p * p + q * q;
  float d = 1.0 + r2 * (inverseDistortion.x + r2 * (inverseDistortion.y + r2 * (inverseDistortion.z + r2 * inverseDistortion.w)));
  position.x = ((p * d - noLensFrustum[LEFT]) / (noLensFrustum[RIGHT] - noLensFrustum[LEFT]) * 2.0 - 1.0) * w * flip;
  position.y = ((q * d - noLensFrustum[BOTTOM]) / (noLensFrustum[TOP] - noLensFrustum[BOTTOM]) * 2.0 - 1.0) * w;

  _color4 = decodeColor();
  _coord2 = color4.xy;
  gl_Position = position;
}

export void worldFragment() {
  vec4 sample = texture2D(texture, _coord2);
  gl_FragColor = _color4 * (1.0 - 0.1 * float(sample.x > 0.6));
}
