namespace App {
  const POSITION_F3 = GPU.VertexFormat.new
    .add(GLSLX_NAME_POSITION3, .FLOAT, 3)

  const WORLD_VERTEX_FORMAT = GPU.VertexFormat.new
    .add(GLSLX_NAME_POSITION3, .FLOAT, 3)
    .add(GLSLX_NAME_TEXTURE4, .BYTE, 4)
    .add(GLSLX_NAME_LIGHTMAP4, .BYTE, 4)
    .add(GLSLX_NAME_CENTER4, .BYTE, 4)

  const EYE_SEPARATION_SCALE = 0.1
  const DISSOLVE_DISTANCE = 3.0
}

class App :: HeadsetDelegate {
  const _context GPU.Context
  const _pool GPU.BufferPool
  const _texture GPU.Texture
  const _lightmap GPU.Texture
  const _lines Lines
  const _voxels Voxels
  const _instructions = document.getElementById("instructions")

  # Modes
  const _playMode PlayMode
  const _editMode EditMode
  var _mode Mode

  # Headsets
  var _passThroughHeadset PassThroughHeadset = null
  var _googleCardboardHeadset GoogleCardboardHeadset = null
  var _headset Headset = null

  # Input info
  const _inputState = InputState.new
  var _fps = 0.0

  # Camera info
  const _modelviewMatrix = Matrix.new
  const _projectionMatrix = Matrix.new
  const _eye = Vector3.new

  # Camera orientation info
  const _orientationMatrix = Matrix.new
  var _deviceAlpha = 0.0
  var _deviceAlphaRate = 0.0
  var _deviceBeta = 0.0
  var _deviceBetaRate = 0.0
  var _deviceGamma = 0.0
  var _deviceGammaRate = 0.0
  var _hasDeviceOrientation = false
  var _mouseAngleX = 0.0
  var _mouseAngleY = 0.0
  var _screenOrientation = 0.0

  def new(context GPU.Context, file Uint8Array) {
    _context = context
    _pool = GPU.BufferPool.new(context)
    _texture = context.createTextureFromURL(.LUMINANCE_LINEAR_REPEAT, "texture.png")
    _lightmap = context.createTextureFromURL(.RGBA_LINEAR_CLAMP, "lightmap.png")
    _lines = Lines.new(context, _pool)
    _voxels = Voxels.new(context, _pool, _lightmap)
    _voxels.loadFromFile(DataArray.new(file), .CELLS_AND_MESH)
    _editMode = EditMode.new(_voxels)
    _playMode = PlayMode.new(_voxels)
    _mode = _playMode
    _passThroughHeadset = PassThroughHeadset.new(_context, self)
  }

  def handleKeyPress(key Key, shift bool) {
    if key == .P {
      var cameraTarget = _mode.cameraTarget
      _mode = _mode is EditMode ? _playMode : _editMode
      _mode.cameraTarget = cameraTarget
      _instructions.style.display = _mode is PlayMode ? "none" : "block"
    } else {
      _mode.handleKeyPress(key, shift)
    }
  }

  def inputState InputState {
    return _inputState
  }

  def updateScreenOrientation(degrees double) {
    _screenOrientation = degrees
  }

  def updateDeviceOrientation(alphaDegrees double, betaDegrees double, gammaDegrees double) {
    _deviceAlpha = alphaDegrees
    _deviceBeta = betaDegrees
    _deviceGamma = gammaDegrees
    _hasDeviceOrientation = true
  }

  def updateDeviceOrientationRate(alphaRateDegrees double, betaRateDegrees double, gammaRateDegrees double) {
    _deviceAlphaRate = alphaRateDegrees
    _deviceBetaRate = betaRateDegrees
    _deviceGammaRate = gammaRateDegrees
  }

  def applyMouseMovement(movementX double, movementY double) {
    const ROTATION_SPEED_IN_RADIANS = 0.005
    _mouseAngleY += movementX * ROTATION_SPEED_IN_RADIANS
    _mouseAngleY -= Math.floor(_mouseAngleY / (2 * Math.PI)) * (2 * Math.PI)
    _mouseAngleX = Math.clamp(_mouseAngleX + movementY * ROTATION_SPEED_IN_RADIANS, -Math.PI / 2, Math.PI / 2)
  }

  def draw {
    _headset = _screenOrientation == 0 ? _passThroughHeadset : _googleCardboardHeadset ?= GoogleCardboardHeadset.new(_context, self, _pool)
    _headset.draw
  }

  def drawEye(eye Eye, projectionMatrix Matrix, worldMaterial GPU.Material) {
    const cameraDistance = 10
    const cameraTarget = _mode.cameraTarget

    _modelviewMatrix
      .loadIdentity
      .translateBy(eye == .LEFT ? EYE_SEPARATION_SCALE : eye == .RIGHT ? -EYE_SEPARATION_SCALE : 0, 0, 0)
      .translateBy(0, 0, -cameraDistance)
      .multiplyBy(_orientationMatrix)
      .translateBy(-cameraTarget.x, -cameraTarget.y, -cameraTarget.z)

    worldMaterial.setUniformSampler(GLSLX_NAME_TEXTURE, _texture, 0)
    worldMaterial.setUniformSampler(GLSLX_NAME_LIGHTMAP, _lightmap, 1)
    worldMaterial.setUniformMat4(GLSLX_NAME_MATRIX_MODELVIEW, _modelviewMatrix)
    worldMaterial.setUniformMat4(GLSLX_NAME_MATRIX_PROJECTION, projectionMatrix)
    worldMaterial.setUniformVec4(GLSLX_NAME_EYE, _orientationMatrix.m20, _orientationMatrix.m21, _orientationMatrix.m22,
      cameraTarget.x * _orientationMatrix.m20 + cameraTarget.y * _orientationMatrix.m21 + cameraTarget.z * _orientationMatrix.m22 + cameraDistance - DISSOLVE_DISTANCE)

    _eye.setTo(
      cameraTarget.x + _orientationMatrix.m20 * cameraDistance,
      cameraTarget.y + _orientationMatrix.m21 * cameraDistance,
      cameraTarget.z + _orientationMatrix.m22 * cameraDistance)

    _mode.draw(_lines)
    _voxels.draw(worldMaterial, _eye, _orientationMatrix)
    _lines.draw(worldMaterial)
  }

  def update(seconds double) {
    if _hasDeviceOrientation {
      # Try to render a frame ahead due to lag inherent in the pipeline
      _deviceAlpha += _deviceAlphaRate * seconds
      _deviceBeta += _deviceBetaRate * seconds
      _deviceGamma += _deviceGammaRate * seconds

      _orientationMatrix
        .loadIdentity

        # Take the screen rotation from the OS into account
        .rotateBy(0, 0, 1, _screenOrientation * DEGREES_TO_RADIANS)

        # Inverse of https://w3c.github.io/deviceorientation/spec-source-orientation.html
        .rotateBy(0, 1, 0, -_deviceGamma * DEGREES_TO_RADIANS)
        .rotateBy(1, 0, 0, -_deviceBeta * DEGREES_TO_RADIANS)
        .rotateBy(0, 0, 1, -_deviceAlpha * DEGREES_TO_RADIANS)

        # Identity should be when the back of the phone faces forward instead of down
        .rotateBy(1, 0, 0, Math.PI / 2)
    }

    else {
      _orientationMatrix
        .loadIdentity
        .rotateBy(1, 0, 0, _mouseAngleX)
        .rotateBy(0, 1, 0, _mouseAngleY)
    }

    # Update the current mode
    _mode.update(_inputState, _orientationMatrix, seconds)

    # Update the FPS indicator
    _fps = _fps * 0.9 + 0.1 / seconds
    (window.top as dynamic).document.title = "\((_fps as dynamic).toFixed(1))fps"
  }

  def openFile(bytes Uint8Array) {
    _voxels.loadFromFile(DataArray.new(bytes), .CELLS_ONLY)
  }

  def saveFile {
    downloadFile("level.lvl", _voxels.saveToFile.bytes)
    downloadFile("lightmap.png", _voxels.saveLightmapToFile)
  }
}

def downloadFile(name string, bytes Uint8Array) {
  var url = URL.createObjectURL(Blob.new([bytes]))
  var link = document.createElementAnchor
  document.body.appendChild(link)
  link.download = name
  link.href = url
  link.click
  document.body.removeChild(link)
  setTimeout(=> URL.revokeObjectURL(url), 1000)
}
